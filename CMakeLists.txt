CMAKE_MINIMUM_REQUIRED( VERSION 2.8 ) 
PROJECT( hnode2-switchd ) 

SET( MAJOR_VERSION 2 )
SET( MINOR_VERSION 2 )
SET( PATCH_VERSION 0 )
SET( RELEASE_VERSION 0 )

SET( DAEMON_NAME hnswitchd )

#SET( CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_SOURCE_DIR}/cmake/Modules/" )

# Default the install prefix to just /usr
IF(CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT)
  SET( CMAKE_INSTALL_PREFIX /usr CACHE PATH "Default to /usr install" FORCE )
ENDIF(CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT)

SET(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} ${CMAKE_SOURCE_DIR}/cmake) 

SET( VERSION ${MAJOR_VERSION}.${MINOR_VERSION}.${PATCH_VERSION} )
SET( DEBVERSION ${VERSION}-${RELEASE_VERSION} )

ADD_DEFINITIONS( -DVERSION=${VERSION} )

SET( HNSWITCHD_SRC
     ${CMAKE_SOURCE_DIR}/src/daemon/hnswitchd.cpp
     ${CMAKE_SOURCE_DIR}/src/daemon/HNSwitchDaemon.cpp
     ${CMAKE_SOURCE_DIR}/src/daemon/HNDaemonLog.cpp
     ${CMAKE_SOURCE_DIR}/src/daemon/HNDaemonHealth.cpp
     ${CMAKE_SOURCE_DIR}/src/daemon/HNScheduleMatrix.cpp
     ${CMAKE_SOURCE_DIR}/src/daemon/HNSwitchManager.cpp
     ${CMAKE_SOURCE_DIR}/src/daemon/HNSWDPacketDaemon.cpp
)

SET( HNSWITCHD_CLIENT_SRC
     ${CMAKE_SOURCE_DIR}/src/client/hnswitchd_client.cpp
     ${CMAKE_SOURCE_DIR}/src/client/HNSWDPacketClient.cpp
)

SET( HNSWITCHD_DEV_HDR
     ${CMAKE_SOURCE_DIR}/src/include/HNSwitchDaemon.h
)

SET(CMAKE_BUILD_TYPE Debug)

FIND_PACKAGE( Poco REQUIRED Util Foundation Net NetSSL JSON )
FIND_PACKAGE( I2C REQUIRED )

INCLUDE_DIRECTORIES( ${CMAKE_SOURCE_DIR}/src/include )

ADD_EXECUTABLE( hnswitchd ${HNSWITCHD_SRC} )
TARGET_LINK_LIBRARIES( hnswitchd PRIVATE 
    ${Poco_Util_LIBRARY}
    ${Poco_Foundation_LIBRARY}
    ${Poco_Net_LIBRARY}
    ${Poco_NetSSL_LIBRARY}
    ${Poco_JSON_LIBRARY} )
TARGET_LINK_LIBRARIES( hnswitchd PRIVATE ${I2C_DEVICE_LIBRARY} )

ADD_EXECUTABLE( hnswitchd_client ${HNSWITCHD_CLIENT_SRC} )
TARGET_LINK_LIBRARIES( hnswitchd_client PRIVATE 
    ${Poco_Util_LIBRARY}
    ${Poco_Foundation_LIBRARY}
    ${Poco_Net_LIBRARY}
    ${Poco_NetSSL_LIBRARY}
    ${Poco_JSON_LIBRARY} )

INSTALL( TARGETS hnswitchd DESTINATION ${CMAKE_INSTALL_PREFIX}/sbin COMPONENT daemon )

CONFIGURE_FILE( ${CMAKE_SOURCE_DIR}/pkg/hnswitchd.service.in ${CMAKE_BINARY_DIR}/hnswitchd.service @ONLY )
INSTALL( FILES ${CMAKE_BINARY_DIR}/hnswitchd.service DESTINATION /etc/systemd/system/ COMPONENT daemon )

INSTALL( TARGETS hnswitchd_client DESTINATION ${CMAKE_INSTALL_PREFIX}/bin COMPONENT client )

INSTALL( FILES ${HNSWITCHD_DEV_HDR} DESTINATION ${CMAKE_INSTALL_PREFIX}/include/hnode2 COMPONENT dev )

SET( CPACK_GENERATOR "DEB" )

SET( CPACK_DEB_COMPONENT_INSTALL 1 )
SET( CPACK_COMPONENTS_ALL daemon client dev )

SET( CPACK_PACKAGE_VERSION_MAJOR ${MAJOR_VERSION} )
SET( CPACK_PACKAGE_VERSION_MINOR ${MINOR_VERSION} )
SET( CPACK_PACKAGE_VERSION_PATCH ${PATCH_VERSION} )

SET( CPACK_PACKAGE_DESCRIPTION "Provides a interface and scheduling for control of low level switching devices such as i2c gpio expanders." )
SET( CPACK_PACKAGE_DESCRIPTION_SUMMARY "The hnode2 low-level switch-daemon" )
SET( CPACK_PACKAGE_CONTACT "Curtis Nottberg" )
SET( CPACK_PACKAGE_ARCHITECTURE DEB-DEFAULT )

SET( CPACK_DEBIAN_PACKAGE_PRIORITY "optional" )
SET( CPACK_DEBIAN_PACKAGE_SECTION "Daemon" )
SET( CPACK_DEBIAN_PACKAGE_MAINTAINER "Curtis Nottberg" ) #required
SET( CPACK_DEBIAN_PACKAGE_VERSION ${DEBVERSION} )
SET( CPACK_DEBIAN_FILE_NAME DEB-DEFAULT )

INCLUDE(CPack)

